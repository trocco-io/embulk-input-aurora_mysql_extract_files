plugins {
    id "java"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "3.1.0"
}

repositories {
    mavenCentral()
}

group = "trocco-io"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Reads files stored on Aurora MySQL Extract Files."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "org.embulk:embulk-util-file:0.2.0"
    implementation "org.embulk:embulk-util-retryhelper:0.9.0"
    implementation "com.amazonaws:aws-java-sdk-s3:1.11.714"
    implementation "mysql:mysql-connector-java:5.1.44"

    testImplementation "junit:junit:4.+"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial"
}

embulkPlugin {
    mainClass = "org.embulk.input.aurora_mysql_extract_files.AuroraMySQLExtractFilesFileInputPlugin"
    category = "input"
    type = "aurora_mysql_extract_files"
}

gem {
    authors = [ "kekekenta" ]
    email = [ "bell.kenta@gmail.com" ]
    summary = "Aurora MySQL Extract Files file input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-aurora_mysql_extract_files"
    licenses = [ "MIT" ]
}
